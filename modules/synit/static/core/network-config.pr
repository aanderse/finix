# Dataspace of intended network configuration.
let ?network = dataspace
$network ? ?x [
  $log ! <log "-" { line: "network" |+++|: $x }>
  ?- $log ! <log "-" { line: "network" |---|: $x }>
]

<require-service
  <config-watcher "/etc/syndicate/network" { config: $network log: $log }>>
<require-service
  <config-watcher "/run/etc/syndicate/network" { config: $network log: $log }>>

# Dataspace of actual network state.
? <machine-dataspace ?machine> [
  $config += <network-dataspace $network $machine>

  # Announce a network milestone while an address with global scope is present.
  $machine ? <address _ _ {"scope": "global"}> [
    $config += <run-service <milestone network>>
  ]

  ? <service-object <daemon network-configurator> ?obj> [
    # The configurator can only observe $network and
    # only assert <address> or <route> into $machine.
    $obj += <network-dataspace
      <* $network [ <reject <not <rec Observe>>> ]>
      <* $machine [<reject <and <not<rec address>> <not<rec route>>>> ]>>
  ]
]
